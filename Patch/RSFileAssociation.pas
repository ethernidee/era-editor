unit RSFileAssociation;

{ *********************************************************************** }
{                                                                         }
{ RSPak                                    Copyright (c) Rozhenko Sergey  }
{ http://sites.google.com/site/sergroj/                                   }
{ sergroj@mail.ru                                                         }
{                                                                         }
{ This file is a subject to any one of these licenses at your choice:     }
{ BSD License, MIT License, Apache License, Mozilla Public License.       }
{                                                                         }
{ *********************************************************************** }
{$I RSPak.inc}

interface

uses
  SysUtils, Windows, RSRegistry, RSQ;

type
  TRSFileAssociation = class(TObject)
  private
    FAssociationName: string;
    FBackupName: string;
    FExtension: string;
    FCommand: string;
    FDefaultIcon: string;
    function GetAssociated: boolean;
    procedure SetAssociated(const Value: boolean);
  public
    constructor Create(const Extension, AssociationName, BackupName,
      Command, DefaultIcon: string);

    property Associated: boolean read GetAssociated write SetAssociated;
    property AssociationName: string read FAssociationName write FAssociationName;
    property BackupName: string read FBackupName write FBackupName;
    property Extension: string read FExtension write FExtension;
    property Command: string read FCommand write FCommand;
    property DefaultIcon: string read FDefaultIcon write FDefaultIcon;
  end;

implementation

uses Registry;

{ TRSFileAssociation }

constructor TRSFileAssociation.Create(const Extension, AssociationName,
   BackupName, Command, DefaultIcon: string);
begin
  FExtension:= Extension;
  FAssociationName:= AssociationName;
  FBackupName:= BackupName;
  FCommand:= Command;
  FDefaultIcon:= DefaultIcon;
end;

function TRSFileAssociation.GetAssociated: boolean;
var s:string;
begin
  with TRSRegistry.Create do
  try
    RootKey:= HKEY_CLASSES_ROOT;
    result:= OpenKeyReadOnly('\' + Extension) and Read('', s) and
             (s = AssociationName) and
             OpenKeyReadOnly('\' + AssociationName + '\shell\open\command') and
             Read('', s) and SameText(s, Command);
  finally
    Free;
  end;
end;

procedure TRSFileAssociation.SetAssociated(const Value: boolean);
var
  s:string; Info:TRegKeyInfo;
begin
  with TRSRegistry.Create do
  try
    RootKey:=HKEY_CLASSES_ROOT;

    if Value then
    begin
      Win32Check(OpenKey('\' + AssociationName + '\DefaultIcon', true));
      WriteString('', DefaultIcon);
      Win32Check(OpenKey('\' + AssociationName + '\shell\open\command', true));
      WriteString('', Command);

      Win32Check(OpenKey('\' + Extension, true));
      if not Read('', s) then
        DeleteValue(BackupName)
      else
        if s<>AssociationName then
          WriteString(BackupName, s);

      WriteString('', AssociationName);
    end else
    begin
      if not OpenKey('\' + Extension, false) then  exit;
      s:= ReadString('');
      if s = AssociationName then
      begin
        if Read(BackupName, s) then
          WriteString('', s)
        else
          DeleteValue('');
          
        DeleteValue(BackupName);
        if GetKeyInfo(Info) and (Info.NumSubKeys or Info.NumValues = 0) then
        begin
          CloseKey;
          DeleteKey('\' + Extension);
        end;

        DeleteKey('\' + AssociationName);
      end;
    end;
  finally
    Free;
  end;
end;

end.
