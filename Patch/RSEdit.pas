unit RSEdit;

{ *********************************************************************** }
{                                                                         }
{ RSPak                                    Copyright (c) Rozhenko Sergey  }
{ http://sites.google.com/site/sergroj/                                   }
{ sergroj@mail.ru                                                         }
{                                                                         }
{ This file is a subject to any one of these licenses at your choice:     }
{ BSD License, MIT License, Apache License, Mozilla Public License.       }
{                                                                         }
{ *********************************************************************** }
{$I RSPak.inc}

interface

uses
  Windows, Messages, SysUtils, Classes, Controls, StdCtrls, RSCommon, RSQ;

{$I RSWinControlImport.inc}

type
  TRSEdit = class(TEdit)
  private
    FOnCreateParams: TRSCreateParamsEvent;
    FProps: TRSWinControlProps;
    FSelAnchor: int;
    function GetCaret:int;
    procedure SetCaret(v:int);
  protected
    procedure CreateParams(var Params:TCreateParams); override;
    procedure TranslateWndProc(var Msg:TMessage);
    procedure WndProc(var Msg:TMessage); override;

    procedure KeyPress(var Key:char); override;
  public
    constructor Create(AOwner:TComponent); override;
    procedure GetSelection(var Start, Caret: integer);
    procedure SetSelection(Start, Caret:int);
    property Caret: int read GetCaret write SetCaret;
  published
    property Align;
    property OnCanResize;
    property OnResize;
    {$I RSWinControlProps.inc}
  end;
  
procedure register;

implementation

procedure register;
begin
  RegisterComponents('RSPak', [TRSEdit]);
end;

{
********************************** TRSEdit ***********************************
}
constructor TRSEdit.Create(AOwner:TComponent);
begin
  inherited Create(AOwner);
  WindowProc:=TranslateWndProc;
end;

procedure TRSEdit.CreateParams(var Params:TCreateParams);
begin
  inherited CreateParams(Params);
  if Assigned(FOnCreateParams) then FOnCreateParams(Params);
end;

function TRSEdit.GetCaret: int;
begin
  result:= RSEditGetCaret(self, FSelAnchor);
end;

procedure TRSEdit.SetCaret(v:int);
begin
  Perform(EM_SETSEL, v, v);
end;

procedure TRSEdit.GetSelection(var Start, Caret: integer);
begin
  RSEditGetSelection(self, Start, Caret, FSelAnchor);
end;

procedure TRSEdit.SetSelection(Start, Caret: int);
begin
  RSEditSetSelection(self, Start, Caret);
end;

procedure TRSEdit.TranslateWndProc(var Msg:TMessage);
var b:boolean;
begin
  if assigned(FProps.OnWndProc) then
  begin
    b:=false;
    FProps.OnWndProc(Self, Msg, b, WndProc);
    if b then exit;
  end;
  WndProc(Msg);
end;

procedure TRSEdit.WndProc(var Msg:TMessage);
begin
  RSProcessProps(self, Msg, FProps);
  inherited;
  RSEditWndProcAfter(self, Msg, FSelAnchor);
end;

procedure TRSEdit.KeyPress(var Key: char);
begin
  inherited;
  if Key=#1 then // Ctrl+A
  begin
    SelectAll;
    Key:=#0;
  end;
end;

end.
